// sqf
//
// Файл настроек "sqf-калькулятора"
//

//
// Дополнительные пункты истории, любые полезные строчки кода
//

_myHistory = [
    // Показывает все магазины используемые турелями техники под курсором
    {"Show turrets weapons"; cursorTarget call SQFCALC_GetTurretsWeapons},
    // Этот watcher постоянно выводит инфу об объекте под курсором
    // активируется по нажатию кнопки "Следить" или по Ctrl+Enter (если из истории то сначала Insert и потом Ctrl+Enter)
    {"Watcher CursorTarget"; call SQFCALC_CursorTargetWatcher},
    // Показывает конфиг объекта под курсором
    {"Config of cursor target"; configFile >> "CfgVehicles" >> typeof cursorTarget},
    // Показывает все оружие которое есть в игре
    {"Show all game weapons"; _a = []; _c = configFile >> "CfgWeapons"; for "_i" from 0 to count _c -1 do { _x = _c select _i; if( isClass _x ) then { if( getNumber (_x >> "scope") > 0 ) then { _a set [count _a, configName _x] } }; }; _a},
    // Показывает все автомобили которые есть в игре
    {"Show all game cars"; _a = []; _c = configFile >> "CfgVehicles"; for "_i" from 0 to count _c -1 do {_x = _c select _i; if( isClass _x ) then { if( getText (_x >> "simulation") == "car" && getNumber (_x >> "scope") > 0 ) then { _a set [count _a, configName _x] } }; }; _a},
    // Показывает анимации объекта в котором сидит игрок
    {"Show animation sources"; configFile >> "cfgVehicles" >> typeof vehicle player >> "AnimationSources"},
    // Показывает анимации объекта на который смотрит игрок
    {"Show animation sources"; configFile >> "cfgVehicles" >> typeof cursorTarget >> "AnimationSources"},
    // Показывает анимации объекта под курсором
    {"Show animation sources of cursor target"; configFile >> "cfgVehicles" >> typeof cursorTarget >> "AnimationSources"},
    // Показывает все ближайшие объекты
    // {"Show nearest objects"; _c = []; { _c set [count _c, [_x, typeof _x]] } foreach nearestObjects [player, [], 20]; _c},
    {"Show nearest objects"; _c = []; { _c set [count _c, [str _x, if(isClass(configFile >> "CfgVehicles" >> typeof _x)) then { getText(configFile >> "CfgVehicles" >> typeof _x >> "model") } else { str _x call SQFCALC_GetModelName }, typeof _x]] } foreach nearestObjects [player, [], 20]; _c},
    {"Show crew roles"; cursorTarget call SQFCALC_GetCrewRoles},
    // Показывает конфиг оружия игрока
    {"Show primary weapon player"; configFile >> "CfgWeapons" >> primaryWeapon player},
    // Показывает конфиг гранатомета игрока
    {"Show secondary weapon player"; configFile >> "CfgWeapons" >> secondaryWeapon player},
    // Показывает все idd существующих в данный момент дисплеев и idc их контролов
    {"Find all display"; [1000, 1000] call SQFCALC_FindAllDisplay},
    // Включает телепортиртацию группы игрока по клику на карте
    {"Teleport player group"; onMapSingleClick {{ _x setpos _pos } foreach units player; true}},
    {"env1" call compile preprocessFileLineNumbers "\userconfig\SQFCalculator\envs\set.sqf"},
    {[configFile >> "CfgVehicles"] call compile preprocessFileLineNumbers "\userconfig\SQFCalculator\dumpConfig.sqf"},
    // Сниппеты
    {"Ikarus" call SQFCALC_findClassInConfig},
    {cursorTarget setObjectTexture [0, "tex\"]},
    {_car = "" createVehicleLocal (screenToWorld [.5,.5]); _car setObjectTexture [0, ""]; player reveal _car},
    {configFile >> "cfgVehicles" >> typeof cursorTarget >> "HitPoints"},
    {cursorTarget setHit [getText (configFile >> "cfgVehicles" >> typeof cursorTarget >> "HitPoints" >> "HitVRotor" >> "name"), 1]},
    {call compile preprocessFileLineNumbers ""}
];

// всякие исследовательские функи
SQFCALC_findClassInConfig = {
    private ["_classname", "_traverse", "_result"];
    _classname = _this;
    _result = [];
    _traverse = {
        private "_confName";
        if( isClass _this ) exitwith {
            if(configName _this == _classname) then {
                _result set [count _result, [str _this, _this]];
            };
            for "_i" from 0 to count _this - 1 do {
                _this select _i call _traverse
            };
        };
    };
    configFile call _traverse;
    _result;
};

SQFCALC_CursorTargetWatcher = {
    private ["_type", "_conf"];
    if( ! isNull cursorTarget ) then {
        _type = typeof cursorTarget;
        _conf = configFile >> "CfgVehicles" >> _type;
        SQFCALC_TargetCursorConf = _conf;
        format [
            "%1<br />class %2 : %3<br />%4<br />%5",
            getText(_conf >> "displayName"),
            _type,
            configName inheritsFrom _conf,
            getText(_conf >> "model"),
            if( getNumber(_conf >> "isMan") == 1 ) then {
                name cursorTarget
            } else {
                getText(_conf >> "Library" >> "libTextDesc")
            }
        ];
    } else {
        "пусто";
    }
};

SQFCALC_GetCrewRoles = {
    private "_list";
    _list = [];
    {
        _list set [count _list, [
            rank _x,
            "Assign as " + (vehicle _x call {
                if (_x == assignedCommander _this) exitwith { "Commander" };
                if (_x == assignedDriver _this) exitwith { "Driver" };
                if (_x == assignedGunner _this) exitwith { "Gunner" };
                if (_x in assignedCargo _this) exitwith { "Cargo" };
                "unknown role"
            }),
            assignedVehicleRole _x
        ]]
    } foreach crew _this;
    _list;
};

SQFCALC_GetTurretsWeapons = {
    private ["_weapons",  "_result", "_getAnyMagazines", "_findRecurse"];
    _weapons = [];
    _result = [];
    _getAnyMagazines = {
        private ["_weapon", "_mags"];
        _weapon = configFile >> "CfgWeapons" >> _this;
        _mags = [];
        {
            _mags = _mags + getArray (
                ( if(_x == "this")then{ _weapon }else{ _weapon >> _x } ) >> "magazines"
            )
        } foreach getArray (_weapon >> "muzzles");
        _mags
    };
    _findRecurse = {
        for "_i" from 0 to count _this -1 do {
            (_this select _i) call {
                if( isClass _this ) then {
                    _weapons = _weapons + getArray ( _this >> "weapons" );
                    (_this >> "turrets") call {
                        if( isClass _this ) then _findRecurse;
                    };
                };
            };
        };
    };
    (
        configFile >> "CfgVehicles" >> (
            switch typeName _this do {
                case "STRING" : {_this};
                case "OBJECT" : {typeOf _this};
                default {nil}
            }
        ) >> "turrets"
    ) call _findRecurse;
    {
        _result set [count _result, [
            _x, _x call _getAnyMagazines
        ]];
    } foreach _weapons;
    _result;
};

SQFCALC_GetModelName = {
    _this = toArray _this;
    for "_i" from 0 to count _this - 1 do {
        if( _this select _i == 32 ) exitwith {
            _this set [_i, 62];
            str parseText ("< " + toString _this);
        };
    };
};

SQFCALC_FindAllDisplay = {
    private ["_displayList", "_display", "_ctrlList", "_ctrl", "_n", "_m"];
    _n = if(count _this > 0) then { _this select 0 };
    _m = if(count _this > 1) then { _this select 1 };
    _displayList = [];
    for "_i" from 0 to 1000 do {
        _display = findDisplay _i;
        if( ! isNull _display ) then {
            _ctrlList = [];
            _displayList set [count _displayList, [_display, _ctrlList]];
            for "_i" from 0 to 1000 do {
                _ctrl = _display displayCtrl _i;
                if( ! isNull _ctrl ) then {
                    _ctrlList set [count _ctrlList, _ctrl];
                };
            };
        };
    };
    _displayList;
};

SQFCALC_RC = {
    private ["_codestr", "_logic"];
    _codestr = "call " + str _this;
    _logic = "logic" createVehicleLocal [0,0];
    _logic setVehicleInit _codestr;
    processInitCommands;
    clearVehicleInit _logic;
    deleteVehicle _logic;
};

//
// Максимальное время, отводимое на выполнение выражения.
// Например вывод полного конфига игры может занять 20 и более секунд. Точнее, на последных патчах армы, ну ооочень много времени :((
//

_maxTimeout = 120;

//
// Коды горячих клавиш, работают с зажатым alt
//
//_HKInput = 2;       // поле ввода sqf-выражения
//_HKDisplay = 3;     // экран с отформатированным результатом
//_HKDisplay2 = 4;    // экран редактирования результата
//_HKHistory = 0x23;  // экран истории
//_HKProcesses = 0x24 // экран прибития watcher'ов
//_HKDemo = 0x20;     // экран html-демок (скриптов, команд, итк)
//_HKHelp = 0x3B;     // экран помощи
//_HKLeft =           // переключить на соседний левый экран
//_HKRight =          // переключить на соседний правый экран
//

//
// Casual shortcut
//

cfg = configFile;
cfgA = configFile >> "CfgAmmo";
cfgM = configFile >> "CfgMagazines";
cfgW = configFile >> "CfgWeapons";
cfgV = configFile >> "CfgVehicles";
